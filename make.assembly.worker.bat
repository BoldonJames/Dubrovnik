echo off
REM
REM This tool is designed to be called from a separate batch for each component assembly
REM

REM
REM Identify target assembly
REM
IF "%~1"=="" (
    Echo "cmd parameter 1 : Assembly name missing"
    exit /B
)
set "ASSEMBLY=%1"

REM
REM Welcome
REM
echo XML and bindings will be generated in the current directory.
set "MY_PATH=%cd%"
echo Current directory = %cd%

REM
REM Set path to target .NET version
REM
set "NET_PATH=C:\Windows\Microsoft.NET\Framework\v4.0.30319"

REM
REM Set Dubrovnik tool exe relative paths
REM
set "REFLECTOR_EXE=.\..\..\dotNET\Dubrovnik.Tools\Dubrovnik.Reflector\bin\Debug\Dubrovnik.Reflector.exe"
set "GENERATOR_EXE=.\..\..\dotNET\Dubrovnik.Tools\Dubrovnik.Generator\bin\Debug\Dubrovnik.Generator.exe"

REM
REM Generate assembly reflection XML file using the Dubrovnik reflector tool
REM
set "REF_INPUT_FILE=%NET_PATH%\%ASSEMBLY%.dll"
set "REF_OUTPUT_FILE=%MY_PATH%\%ASSEMBLY%.xml"
echo.
echo Generating reflection XML file 
echo.
REM
REM Delete exiting target XML file
REM
if exist %REF_OUTPUT_FILE% (
    echo Deleting %REF_OUTPUT_FILE%
    del "%REF_OUTPUT_FILE%" /q
)

REM
REM Run the reflector to generate assembly XML
REM
"%REFLECTOR_EXE%" -i "%REF_INPUT_FILE%" -o "%MY_PATH%" -v true

REM
REM Generate Obj-C code bindings using the Dubrovnik generator tool.
REM The input is the file generated by the reflector tool above.
REM
set "GEN_INPUT_FILE=%REF_OUTPUT_FILE%"
set "GEN_OUTPUT_PATH=%MY_PATH%\Generated Bindings"
echo.
echo Generating bindings
echo.
REM
REM Delete the output generator directory
REM
if exist "%GEN_OUTPUT_PATH%\" (
    echo Deleting existing content of %GEN_OUTPUT_PATH%\
    del "%GEN_OUTPUT_PATH%\*.*" /q
)

REM
REM Run the generator to form Obj-C bindings from assembly XML definition
REM
"%GENERATOR_EXE%"  -i "%GEN_INPUT_FILE%" -o "%GEN_OUTPUT_PATH%" -v true